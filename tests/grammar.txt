<RE>        ::=   <union> | <simple>
<union>     ::=   <RE> "|" <simple>
<simple>    ::=   <concat> | <basic>
<concat>    ::=   <simple> <basic>
<basic>     ::=   <zero> | <one> | <zeroone> | <rep> | <elem>
<zero>      ::=   <elem> "*"
<one>       ::=   <elem> "+"
<zeroone>   ::=   <elem> "?"
<rep>       ::=   <elem> "{" <reps> "}"
<reps>      ::=   <rep_n> | <rep_range> | <rep_more> | <rep_less>
<rep_n>     ::=   <integer>
<rep_range> ::=   <integer> "," <integer>
<rep_more>  ::=   <integer> ","
<rep_less>  ::=   "," <integer>
<elem>      ::=   <group> | <any> | <eol> | <sol> | <char> | <set>
<group>     ::=   "(" <RE> ")"
<any>       ::=   "."
<sol>       ::=   "^"
<eol>       ::=   "$"
<char>      ::=   any non metacharacter | "\" metacharacter
<set>       ::=   "[" <set-items> "]"
<set-items> ::=   <set-item> | <set-item> <set-items>
<set-item>  ::=   <range> | <char>
<range>     ::=   <char> "-" <char>
